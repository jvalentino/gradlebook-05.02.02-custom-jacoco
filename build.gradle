// PLUGINS
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'jacoco'
apply plugin: 'maven'

// CODENARC
codenarc {
	toolVersion = "1.1"
}

codenarcMain {
	configFile = file("codenarc/CodeNarcMain.groovy")
	reports {
		html.enabled = true
		xml.enabled = true
	}
}

codenarcTest {
	configFile = file("codenarc/CodeNarcTest.groovy")
}

// JACOCO
jacocoTestReport {
    doFirst {
        classDirectories = classDirectories.collect { 
            fileTree(it) { exclude '**/*$*_closure*' } 
        }.sum()
    }
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}
}

jacocoTestCoverageVerification {
    doFirst {
        classDirectories = classDirectories.collect { 
            fileTree(it) { exclude '**/*$*_closure*' } 
        }.sum()
    }
}

// GROOVY
version = '1.0.0'
group = 'com.blogspot.jvalentino.gradle'
archivesBaseName = 'custom-jacoco'

repositories {
    jcenter()
}

dependencies {
	compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile group: 'cglib', name: 'cglib', version: '3.2.6'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.6'
}

// MAVEN
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url:
                "http://localhost:8081/artifactory/gradle-release") {
                authentication(
                    userName: "admin", password: "changeit")
            }
        }
    }
}
